defpackage ao :
  import core
  import math
  import collections
  import utils
  import glu
  import geom
  import font

extern tree_x: () -> ptr<?>
extern tree_y: () -> ptr<?>
extern tree_z: () -> ptr<?>
extern tree_const: (float) -> ptr<?>
extern tree_unary: (int, ptr<?>) -> ptr<?>
extern tree_binary: (int, ptr<?>, ptr<?>) -> ptr<?>
extern opcode_enum: (ptr<?>) -> int
extern tree_export_slice: (ptr<?>, ptr<?>, float, float, float, float, float, float) -> int
extern tree_export_mesh: (ptr<?>, ptr<?>, float, float, float, float, float, float, float) -> int
;; extern tree_render_mesh: (ptr<?>, ptr<ptr<float>>, float, float, float, float, float, float, float) -> int
extern tree_render_mesh: (ptr<?>, ptr<int>, ptr<ptr<float>>, ptr<int>, ptr<ptr<int>>, float, float, float, float, float, float, float) -> int
extern tree_render_slice: (ptr<?>, float, float, float, float, float, float) -> ptr<Contours>
extern contours_delete: (ptr<Contours>) -> int
extern window_show_tree: (ptr<?>, ptr<?>, ptr<?>) -> ptr<?>
extern ao_run: () -> ptr<?>
extern ao_halt: () -> ptr<?>
extern printf: (ptr<?>, ? ...) -> int
extern free: (ptr<?>) -> int

public lostanza deftype Contours :
  xs : ptr<ptr<float>>
  ys : ptr<ptr<float>>
  sizes : ptr<int>
  size : int

public lostanza deftype Tree <: Geom :
  value: ptr<?>

defmethod print (o:OutputStream, t:Tree) :
  print-all(o, ["Tree()"])

public deftype FloField <: Equalable
public defmulti tree (f:FloField) -> Tree
public defmulti count (f:FloField) -> Int

public defstruct FloFieldLit <: FloField :
  tree: Tree with: (as-method => true)
  value: Float

defn lit? (f:FloField) -> True|False :
  match(f):
  (l:FloFieldLit) : true
  (o:FloFieldOp) : false

defn lit? (f:FloField, v:Float) -> True|False :
  match(f):
  (l:FloFieldLit) : value(l) == v
  (o:FloFieldOp) : false

defn lit-value (f:FloField) -> Float :
  match(f):
  (l:FloFieldLit) : value(l)
  (o:FloFieldOp) : 0.0f

defmethod print (s:OutputStream, f:FloFieldLit) :
  print(s, "LIT(%_)" % [value(f)])

defmethod equal? (a:FloFieldLit, b:FloFieldLit) -> True|False :
  value(a) == value(b)

defmethod count (a:FloFieldLit) -> Int :
  1

public defstruct FloFieldOp <: FloField :
  tree: Tree with: (as-method => true)
  op: String
  args: List<FloField>

defmethod print (s:OutputStream, f:FloFieldOp) :
  print(s, "%_(%,)" % [op(f), args(f)])

defmethod equal? (a:FloFieldOp, b:FloFieldOp) -> True|False :
  op(a) == op(b) and all?(equal?, args(a), args(b))

defmethod count (f:FloFieldOp) -> Int :
  1 + reduce(plus, 0, seq(count, args(f)))

public deftype Poseable <: Geom
public defmulti bounds (p:Poseable) -> Box
public defmulti color (p:Poseable) -> V4f
public defmulti points (p:Poseable) -> Tuple<V3f>
public defmulti xyz<?T> (mat:Mat44, p:?T&Poseable) -> T
public defmulti rgb<?T> (c:V4f, p:?T&Poseable) -> T
public defmulti shapes (p:Poseable) -> Seqable<Shape>

public defn mov-x<?T> (dx:Float, p:?T&Poseable) -> T : mov(V3f(dx, 0.0f, 0.0f), p)
public defn mov-y<?T> (dy:Float, p:?T&Poseable) -> T : mov(V3f(0.0f, dy, 0.0f), p)
public defn mov-z<?T> (dz:Float, p:?T&Poseable) -> T : mov(V3f(0.0f, 0.0f, dz), p)

;;; ASSEMBLIES

public deftype Assembly <: Poseable
public defmulti children (a:Assembly) -> Seqable<Poseable>
public defmulti mat (l:Assembly) -> Mat44

public defn Assembly (mat:Mat44, color:V4f, poseables:Seqable<Poseable>) -> Assembly :
  val children = to-tuple $ poseables
  new Assembly :
    defmethod children (this) : children
    defmethod bounds (this) : mat * reduce(bit-or, seq(bounds, children))
    defmethod mat (this) : mat
    defmethod color (this) : color
    defmethod shapes (this) : seq(rgb{color, _}, seq(xyz{mat, _}, cat-all $ seq(shapes, children)))
    defmethod points (this) : to-tuple $ cat-all $ seq(points, children)

public defn Assembly (mat:Mat44, poseables:Seqable<Poseable>) -> Assembly :
  Assembly(mat, V4f(0.0f, 0.0f, 0.0f, 0.0f), poseables)
  
public defn Assembly (poseables:Seqable<Poseable>) -> Assembly :
  Assembly(id-mat44(), poseables)
  
public defn Assembly (color:V4f, poseables:Seqable<Poseable>) -> Assembly :
  Assembly(id-mat44(), color, poseables)

defmethod print (s:OutputStream, a:Assembly) :
  print(s, "Assembly(%,)" % [children(a)])

public defmethod xyz (xf:Mat44, a:Assembly) -> Assembly :
  Assembly(mat(a) * xf, color(a), children(a))

public defmethod rgb (c:V4f, a:Assembly) -> Assembly :
  Assembly(mat(a), color(a), children(a))

public deftype AnyTree <: Geom

public defstruct BoundedTree <: AnyTree :
  bounds : Box
  tree:    Tree

public defstruct TreeAssembly <: AnyTree :
  mat : Mat44
  color : V4f
  trees : Tuple<AnyTree>

;;; SHAPE

public deftype Shape <: Poseable
public defmulti fabber (s:Shape) -> ((Vec3Field) -> FloField)
public defmulti cache (s:Shape) -> HashTable<Vec3Field,FloField>

public defn Shape (fabber:(Vec3Field) -> FloField, bounds:Box, points:Tuple<V3f>) :
  val cache = HashTable<Vec3Field,FloField>()
  new Shape :
    defmethod fabber (this) : fabber
    defmethod bounds (this) : bounds
    defmethod points (this) : points
    defmethod cache (this) : cache
    defmethod shapes (this) : [ this ]

public defn Shape (fabber:(Vec3Field) -> FloField, bounds:Box) :
  Shape(fabber, bounds, [])

public defn add-points (s:Shape, points:Tuple<V3f>) :
  Shape(fabber(s), bounds(s), points)

public defn add-min-x-point (s:Shape) :
  add-points(s, [V3f(x(lo(bounds(s))), 0.0f, 0.0f)])
public defn add-max-x-point (s:Shape) :
  add-points(s, [V3f(x(hi(bounds(s))), 0.0f, 0.0f)])
public defn add-min-y-point (s:Shape) :
  add-points(s, [V3f(0.0f, y(lo(bounds(s))), 0.0f)])
public defn add-max-y-point (s:Shape) :
  add-points(s, [V3f(0.0f, y(hi(bounds(s))), 0.0f)])
public defn add-min-z-point (s:Shape) :
  add-points(s, [V3f(0.0f, 0.0f, z(lo(bounds(s))))])
public defn add-max-z-point (s:Shape) :
  add-points(s, [V3f(0.0f, 0.0f, z(hi(bounds(s))))])

defmethod print (s:OutputStream, o:Shape) :
  print-all(s, ["Shape(" bounds(o) ")"])

defn fab (s:Shape, p:Vec3Field) -> FloField :
  if key?(cache(s), p) :
    cache(s)[p]
  else :
    val f = fabber(s)(p)
    cache(s)[p] = f
    ;; if length(cache(s)) > 1 :
    ;;   println-all(["MISS " to-tuple $ keys(cache(s))])
    f

public defn Shape (s:Shape, bounds:Box, points:Tuple<V3f>) :
  Shape(fabber(s), bounds)

public defn Shape (s:Shape, bounds:Box) :
  Shape(s, bounds, [])

public lostanza defn tree-export-mesh (t:ref<Tree>, filename:ref<String>, box:ref<Box>, res:ref<Float>) -> ref<False> :
  call-c tree_export_mesh(t.value, addr!(filename.chars), x(lo(box)).value, x(hi(box)).value, y(lo(box)).value, y(hi(box)).value, z(lo(box)).value, z(hi(box)).value, res.value)
  return false

public lostanza defn tree-export-slice (t:ref<Tree>, filename:ref<String>, box:ref<Box>, res:ref<Float>) -> ref<False> :
  call-c tree_export_slice(t.value, addr!(filename.chars), x(lo(box)).value, x(hi(box)).value, y(lo(box)).value, y(hi(box)).value, z(lo(box)).value, res.value)
  return false

;; public lostanza deftype RenderMesh :
;;   length: int
;;   value: ptr<float>

public lostanza deftype RenderMesh :
  n-verts: int
  verts: ptr<float>
  n-tris: int
  tris: ptr<int>

public lostanza defn n-verts (m:ref<RenderMesh>) -> ref<Int> :
  return new Int{m.n-verts}

public lostanza defn get-vert-value (m:ref<RenderMesh>, i:ref<Int>) -> ref<Float> :
  return new Float{m.verts[i.value]}

public lostanza defn n-tris (m:ref<RenderMesh>) -> ref<Int> :
  return new Int{m.n-tris}

public lostanza defn get-vert-index (m:ref<RenderMesh>, i:ref<Int>) -> ref<Int> :
  return new Int{m.tris[i.value]}

defn to-mesh (rm:RenderMesh) -> Mesh :
  val tris = generate<V3i> :
    for ti in 0 to n-tris(rm) do :
      val toff = ti * 3
      yield(V3i(get-vert-index(rm, toff + 0), get-vert-index(rm, toff + 1), get-vert-index(rm, toff + 2)))
  val verts = generate<V3f> :
    for vi in 0 to n-verts(rm) do :
      val voff = vi * 3
      yield(V3f(get-vert-value(rm, voff + 0), get-vert-value(rm, voff + 1), get-vert-value(rm, voff + 2)))
  Mesh(to-tuple(verts), to-tuple(tris))

public lostanza defn tree-render-mesh (t:ref<Tree>, box:ref<Box>, res:ref<Float>) -> ref<Mesh> :
  val out = new RenderMesh{0, 0L as ptr<float>, 0, 0L as ptr<int>}
  val len = call-c tree_render_mesh(t.value, addr!(out.n-verts), addr!(out.verts), addr!(out.n-tris), addr!(out.tris), x(lo(box)).value, x(hi(box)).value, y(lo(box)).value, y(hi(box)).value, z(lo(box)).value, z(hi(box)).value, res.value)
  val mesh = to-mesh(out)
  call-c free(out.verts)
  call-c free(out.tris)
  return mesh

lostanza defn get-x (cs:ref<Contours>, c:ref<Int>, i:ref<Int>) -> ref<Float> :
  return new Float{cs.xs[c.value][i.value]}

lostanza defn get-y (cs:ref<Contours>, c:ref<Int>, i:ref<Int>) -> ref<Float> :
  return new Float{cs.ys[c.value][i.value]}

lostanza defn get-contour-size (cs:ref<Contours>, i:ref<Int>) -> ref<Int> :
  return new Int{cs.sizes[i.value]}

lostanza defn num-contours (cs:ref<Contours>) -> ref<Int> :
  return new Int{cs.size}

defn to-polygon (cs:Contours) -> Polygon :
  val contours =
    for ci in 0 to num-contours(cs) seq :
      val points =
        for j in 0 to get-contour-size(cs, ci) seq :
          V2f(get-x(cs, ci, j), get-y(cs, ci, j))
      Contour(to-tuple $ points)
  Polygon(to-tuple $ contours)

public lostanza defn tree-render-slice (t:ref<Tree>, box:ref<Box>, res:ref<Float>) -> ref<Polygon> :
  val out = call-c tree_render_slice(t.value, x(lo(box)).value, x(hi(box)).value, y(lo(box)).value, y(hi(box)).value, z(lo(box)).value, res.value)
  val contours = new Contours{out.xs, out.ys, out.sizes, out.size}
  val poly = to-polygon(contours)
  call-c contours_delete(out)
  return poly

public lostanza defn show (t:ref<Tree>, filename:ref<String>, name:ref<String>) -> ref<False> :
  call-c window_show_tree(addr!(filename.chars), addr!(name.chars), t.value)
  return false

public defn show (t:Tree) :
  show(t, "ao.stanza", "shape")

public lostanza defn ao-run () -> ref<False>  :
  call-c ao_run()
  return false

public lostanza defn ao-halt () -> ref<False> :
  call-c ao_halt()
  return false

public lostanza defn fab-x () -> ref<FloField> :
  return FloFieldOp(new Tree{call-c tree_x()}, String("X"), List())
public lostanza defn fab-y () -> ref<FloField> :
  return FloFieldOp(new Tree{call-c tree_y()}, String("Y"), List())
public lostanza defn fab-z () -> ref<FloField> :
  return FloFieldOp(new Tree{call-c tree_z()}, String("Z"), List())
public lostanza defn lit (f:ref<Float>) -> ref<FloField> :
  return FloFieldLit(new Tree{call-c tree_const(f.value)}, f)
public lostanza defn lit (f:ref<Double>) -> ref<FloField> :
  return lit(to-float(f))
public lostanza defn opcode (name:ref<String>) -> ref<Int> :
  return new Int{call-c opcode_enum(addr!(name.chars))}
public lostanza defn unary (op:ref<String>, t:ref<FloField>) -> ref<FloField> :
  return FloFieldOp(new Tree{call-c tree_unary(opcode(op).value, tree(t).value)}, op, List(t))
public lostanza defn binary (op:ref<String>, t1:ref<FloField>, t2:ref<FloField>) -> ref<FloField> :
  return FloFieldOp(new Tree{call-c tree_binary(opcode(op).value, tree(t1).value, tree(t2).value)}, op, List(t1, t2))
public defn min-lit () : lit(FLOAT-MIN)
public defn max-lit () : lit(FLOAT-MAX)

public defn nan-fill (x:FloField, y:FloField) -> FloField : binary("nan-fill", x, y)
public defn modulo (x:FloField, y:FloField) -> FloField :
  if lit?(x) and lit?(y): lit(modulo(lit-value(x), lit-value(y)))
  else : binary("mod", x, y)
public defn times (x:FloField, y:FloField) -> FloField : 
  if lit?(x) and lit?(y): lit(times(lit-value(x), lit-value(y)))
  else if lit?(x, 1.0f): y else if lit?(y, 1.0f): x 
  else if lit?(x, 0.0f) or lit?(y, 0.0f): lit(0.0f)
  else : binary("mul", x, y)
public defn min (x:FloField, y:FloField) -> FloField : 
  if lit?(x) and lit?(y): lit(min(lit-value(x), lit-value(y)))
  else if lit?(y, FLOAT-MAX): x else if lit?(x, FLOAT-MAX): y
  else : binary("min", x, y)
public defn max (x:FloField, y:FloField) -> FloField :
  if lit?(x) and lit?(y): lit(max(lit-value(x), lit-value(y)))
  else if lit?(y, FLOAT-MIN): x else if lit?(x, FLOAT-MIN): y
  else : binary("max", x, y)
public defn divide (x:FloField, y:FloField) -> FloField : 
  if lit?(x) and lit?(y): lit(divide(lit-value(x), lit-value(y)))
  else if lit?(y, 1.0f): x 
  else if lit?(x, 0.0f): x
  else : lit(0.0f)
public defn plus (x:FloField, y:FloField) -> FloField :
  if lit?(x) and lit?(y): lit(plus(lit-value(x), lit-value(y)))
  else if lit?(x, 0.0f): y else if lit?(y, 0.0f): x else : binary("add", x, y)
public defn minus (x:FloField, y:FloField) -> FloField : 
  if lit?(x) and lit?(y): lit(minus(lit-value(x), lit-value(y)))
  else if lit?(y, 0.0f): x else : binary("sub", x, y)
public defn atan2 (x:FloField, y:FloField) -> FloField :
  if lit?(x) and lit?(y): lit(atan2(lit-value(x), lit-value(y)))
  else if lit?(x) and lit?(y): lit(atan2(lit-value(x), lit-value(y))) else: binary("atan2", x, y)
public defn sqrt (x:FloField) -> FloField :
  if lit?(x): lit(sqrt(lit-value(x))) else: unary("sqrt", x)
public defn sqr (x:FloField) -> FloField :
  if lit?(x): lit(lit-value(x) * lit-value(x)) else: unary("square", x)
public defn abs (x:FloField) -> FloField :
  if lit?(x): lit(abs(lit-value(x))) else: unary("abs", x)
public defn sin (x:FloField) -> FloField : 
  if lit?(x): lit(sin(lit-value(x))) else: unary("sin", x)
public defn cos (x:FloField) -> FloField : 
  if lit?(x): lit(cos(lit-value(x))) else: unary("cos", x)
public defn tan (x:FloField) -> FloField : 
  if lit?(x): lit(tan(lit-value(x))) else: unary("tan", x)
public defn asin (x:FloField) -> FloField : 
  if lit?(x): lit(asin(lit-value(x))) else: unary("asin", x)
public defn acos (x:FloField) -> FloField : 
  if lit?(x): lit(acos(lit-value(x))) else: unary("acos", x)
public defn exp (x:FloField) -> FloField : 
  if lit?(x): lit(exp(lit-value(x))) else: unary("exp", x)
public defn negate (x:FloField) -> FloField : 
  if lit?(x): lit(negate(lit-value(x))) else: unary("neg", x)

public defn clamp (n:FloField, mn:FloField, mx:FloField) -> FloField :
  min(mx, max(mn, n))

public deftype Vec3Field <: Hashable & Equalable
public defmulti x (v:Vec3Field) -> FloField
public defmulti y (v:Vec3Field) -> FloField
public defmulti z (v:Vec3Field) -> FloField
public defmulti id (v:Vec3Field) -> Int

var nxt-id = -1

defn new-id () :
  nxt-id = nxt-id + 1
  nxt-id

public defn Vec3Field (x:FloField, y:FloField, z:FloField) :
  val id = new-id()
  new Vec3Field :
    defmethod x (this) : x
    defmethod y (this) : y
    defmethod z (this) : z
    defmethod id (this) : id
    defmethod hash (this) : id

defmethod equal? (a:Vec3Field, b:Vec3Field) -> True|False :
  x(a) == x(b) and y(a) == y(b) and z(a) == z(b) 

public defn lit (v:V3f) -> Vec3Field :
  Vec3Field(lit(x(v)), lit(y(v)), lit(z(v)))

public defn Vec3Field (n:FloField) -> Vec3Field :
  Vec3Field(n, n, n)

defmethod print (o:OutputStream, v:Vec3Field) :
  print(o, "Vec3Field(%_, %_, %_)" % [x(v), y(v), z(v)])

public deftype Vec2Field <: Vec3Field

public defn Vec2Field (x:FloField, y:FloField) :
  new Vec2Field :
    defmethod x (this) : x
    defmethod y (this) : y
    defmethod z (this) : lit(0.0)

defmethod print (o:OutputStream, v:Vec2Field) :
  print(o, "Vec2Field(%_, %_)" % [x(v), y(v)])

public defn Vec2Field (n:FloField) -> Vec2Field :
  Vec2Field(n, n)

public defn xy (v:Vec3Field) -> Vec2Field :
  Vec2Field(x(v), y(v))

public defn yz (v:Vec3Field) -> Vec2Field :
  Vec2Field(y(v), z(v))

public defn xz (v:Vec3Field) -> Vec2Field :
  Vec2Field(x(v), z(v))

public defn abs (a:Vec3Field) -> Vec3Field :
  Vec3Field(abs(x(a)), abs(y(a)), abs(z(a)))

public defn max (a:Vec3Field, b:FloField) -> Vec3Field :
  Vec3Field(max(x(a), b), max(y(a), b), max(z(a), b))

public defn max (a:Vec3Field, b:Vec3Field) -> Vec3Field :
  Vec3Field(max(x(a), x(b)), max(y(a), y(b)), max(z(a), z(b)))

public defn min (a:Vec3Field, b:FloField) -> Vec3Field :
  Vec3Field(min(x(a), b), min(y(a), b), min(z(a), b))

public defn min (a:Vec3Field, b:Vec3Field) -> Vec3Field :
  Vec3Field(min(x(a), x(b)), min(y(a), y(b)), min(z(a), z(b)))

public defn plus (a:Vec3Field, b:Vec3Field) -> Vec3Field :
  Vec3Field(x(a) + x(b), y(a) + y(b), z(a) + z(b))

public defn minus (a:Vec3Field, b:Vec3Field) -> Vec3Field :
  Vec3Field(x(a) - x(b), y(a) - y(b), z(a) - z(b))

public defn negate (a:Vec3Field) -> Vec3Field :
  Vec3Field((- x(a)), (- y(a)), (- z(a)))

public defn times (s:FloField, v:Vec3Field) -> Vec3Field :
  Vec3Field(s * x(v), s * y(v), s * z(v))

public defn times (a:Vec3Field, b:Vec3Field) -> Vec3Field :
  Vec3Field(x(a) * x(b), y(a) * y(b), z(a) * z(b))

public defn times (m:Mat44, v:Vec3Field) -> Vec3Field :
  val px = x(v) * lit(m[0,0]) + y(v) * lit(m[1,0]) + z(v) * lit(m[2,0]) + lit(m[3,0])
  val py = x(v) * lit(m[0,1]) + y(v) * lit(m[1,1]) + z(v) * lit(m[2,1]) + lit(m[3,1])
  val pz = x(v) * lit(m[0,2]) + y(v) * lit(m[1,2]) + z(v) * lit(m[2,2]) + lit(m[3,2])
  val pw = x(v) * lit(m[0,3]) + y(v) * lit(m[1,3]) + z(v) * lit(m[2,3]) + lit(m[3,3])
  Vec3Field(px / pw, py / pw, pz / pw)

public defn dot (a:Vec3Field, b:Vec3Field) -> FloField :
  x(a) * x(b) + y(a) * y(b) + z(a) * z(b)

public defn len (v:Vec3Field) -> FloField :
  sqrt(dot(v, v))

public defn normalize (v:Vec3Field) -> Vec3Field :
  v / len(v)

public defn divide (a:Vec3Field, b:FloField) -> Vec3Field :
  Vec3Field(x(a) / b, y(a) / b, z(a) / b)

public defn divide (a:Vec3Field, b:Vec3Field) -> Vec3Field :
  Vec3Field(x(a) / x(b), y(a) / y(b), z(a) / z(b))

public defn modulo (a:Vec3Field, b:Vec3Field) -> Vec3Field :
  Vec3Field(y(a) * z(b) - z(a) * y(b),
            z(a) * x(b) - x(a) * z(b),
            x(a) * y(b) - y(a) * x(b))

public defn with-xyz (mat:Mat44, f:(Vec3Field) -> Vec3Field, s:Shape) -> Shape :
  Shape(fn (p): fab(s, f(p)), mat * bounds(s), to-tuple $ seq({mat * _} points(s)))

public defmethod xyz (mat:Mat44, s:Shape) -> Shape :
  with-xyz(mat, fn (p) : inverse(mat) * p, s)

;; TODO
public defmethod rgb (c:V4f, s:Shape) -> Shape :
  s

public defn mov<?T> (dp:V3f, s:?T&Poseable) -> T :
  xyz(mov-mat44(dp), s)

;; public defn mov (dx:FloField, dy:FloField, f:Shape) -> FloField :
;;   mov(dx, dy, lit(0.0), f)

public defn degrees-to-radians (d:Float) -> Float :
  d * to-float(PI-F) / 180.0f

public defn radians-to-degrees (a:Float) -> Float :
  a * 180.0f / to-float(PI-F)

public defn rot-x<?T> (d:Float, s:?T&Poseable) -> T :
  xyz(rot-mat44(V3f(1.0f, 0.0f, 0.0f), degrees-to-radians(d)), s)

public defn rot-y<?T> (d:Float, s:?T&Poseable) -> T :
  xyz(rot-mat44(V3f(0.0f, 1.0f, 0.0f), degrees-to-radians(d)), s)

public defn rot-z<?T> (d:Float, s:?T&Poseable) -> T :
  xyz(rot-mat44(V3f(0.0f, 0.0f, 1.0f), degrees-to-radians(d)), s)

public defn reflect-x<?T> (s:?T&Poseable) -> T : xyz(mag-mat44(V3f(-1.0f, 1.0f, 1.0f)), s)

public defn reflect-y<?T> (s:?T&Poseable) -> T : xyz(mag-mat44(V3f( 1.0f,-1.0f, 1.0f)), s)

public defn reflect-z<?T> (s:?T&Poseable) -> T : xyz(mag-mat44(V3f( 1.0f, 1.0f,-1.0f)), s)

public defn reflect-xy<?T> (s:?T&Poseable) -> T : xyz(reflect-xy-mat44(), s)

public defn reflect-xz<?T> (s:?T&Poseable) -> T : xyz(reflect-xz-mat44(), s)

public defn reflect-yz<?T> (s:?T&Poseable) -> T : xyz(reflect-yz-mat44(), s)

public defn mag<?T> (fac:Float, s:?T&Poseable) -> T : xyz(mag-mat44(fac), s)

public defn mag1<?T> (sa:Float, s:?T&Poseable) -> T : xyz(mag-mat44(sa), s)

public defn mag<?T> (fac:V3f, s:?T&Poseable) -> T : xyz(mag-mat44(fac), s)

public defn mag-x<?T> (sx:Float, s:?T&Poseable) -> T : xyz(mag-mat44(V3f(sx, 1.0f, 1.0f)), s)

public defn mag-y<?T> (sy:Float, s:?T&Poseable) -> T : xyz(mag-mat44(V3f(1.0f, sy, 1.0f)), s)

public defn mag-z<?T> (sz:Float, s:?T&Poseable) -> T : xyz(mag-mat44(V3f(1.0f, 1.0f, sz)), s)

defn shear-x-y (ymin:Float, ymax:Float, dx0:Float, dx1:Float, s:Shape) -> Shape :
  defn xf (p) :
    val f = (y(p) - lit(ymin)) / (lit(ymax) - y(p))
    Vec3Field(x(p) - lit(dx0) * (lit(1.0) - f) - lit(dx1) * f, y(p), z(p))  
  with-xyz(shear-x-y-mat44(ymin, ymax, dx0, dx1), xf, s)

public defn shear-x-y (h:Float, dx0:Float, dx1:Float, s:Shape) -> Shape :
  shear-x-y(dim-min(h), dim-max(h), dx0, dx1, s)

;; TODO : UPDATE BOUNDS
defn taper-x-y (ymin:Float, ymax:Float, s0:Float, s1:Float, s:Shape) -> Shape :
  defn xf (p) :
    val sc = lit(ymax - ymin) / ((lit(s1) * (y(p) - lit(ymin))) - (lit(s0) * (y(p) - lit(ymax))))
    Vec3Field(x(p) * sc, y(p), z(p))
  with-xyz(id-mat44(), xf, s)

;; TODO : UPDATE BOUNDS
defn taper-xy-z (zmin:Float, zmax:Float, s0:Float, s1:Float, s:Shape) -> Shape :
  defn xf (p) :
    val sc = lit(zmax - zmin) / ((lit(s1) * (z(p) - lit(zmin))) - (lit(s0) * (z(p) - lit(zmax))))
    Vec3Field(x(p) * sc, y(p) * sc, z(p))
  with-xyz(id-mat44(), xf, s)

public defn taper-xy-z (h:Float, s0:Float, s1:Float, s:Shape) -> Shape :
  taper-xy-z(dim-min(h), dim-max(h), s0, s1, s)

;; TODO : UPDATE BOUNDS
public defn revolve-y (s:Shape) -> Shape :
  with-xyz(id-mat44(), fn (p): Vec3Field(sqrt(sqr(x(p)) + sqr(z(p))), y(p), z(p)), s)

;; TODO : UPDATE BOUNDS
public defn revolve-x (s:Shape) -> Shape :
  with-xyz(id-mat44(), fn (p): Vec3Field(x(p), sqrt(sqr(y(p)) + sqr(z(p))), z(p)), s)

;; TODO : UPDATE BOUNDS
public defn xy (z:Float, s:Shape) -> Shape :
  with-xyz(id-mat44(), fn (p): Vec3Field(x(p), y(p), lit(z)), s)

public defn xy (s:Shape) -> Shape : xy(0.0f, s)

public defn circle (d:Float) -> Shape :
  val r = d * 0.5f
  Shape(fn (p): len(xy(p)) - lit(r), Box(V3f((- r), (- r), FLOAT-MIN), V3f(r, r, FLOAT-MAX)))

;; public defn circle (x:FloField, y:FloField, r:FloField) -> FloField :
;;   mov(x, y, {circle(r)})

public defn sphere (d:Float) -> Shape :
  val r = d * 0.5f
  Shape(fn (p): len(p) - lit(r), Box(V3f((- r), (- r), (- r)), V3f(r, r, r)))

;; public defn sphere (x:FloField, y:FloField, z:FloField, r:FloField) -> FloField :
;;   mov(x, y, z, {circle(r)})

public defn bit-or (a:Shape, b:Shape) -> Shape :
  Shape(fn (p): min(fab(a, p), fab(b, p)), bounds(a) | bounds(b),
        to-tuple $ cat(points(a), points(b)))

public defn union (args:Seqable<Shape>) -> Shape :
  reduce(bit-or, empty(), args)

public defn bit-and (a:Shape, b:Shape) -> Shape :
  Shape(fn (p): max(fab(a, p), fab(b, p)), bounds(a) & bounds(b),
        to-tuple $ cat(points(a), points(b)))

public defn intersection (args:Seqable<Shape>) -> Shape :
  reduce(bit-and, args)

public defn invert (s:Shape) -> Shape :
  Shape(fn (p): (- fab(s, p)), inf-box())

public defn rem (a:Shape, b:Shape) -> Shape :
  Shape(a & invert(b), bounds(a), points(a))

public defn rem (a:Shape, args:Seqable<Shape>) -> Shape :
  rem(a, union(args))

public defn rem (shapes:Seqable<Shape>) -> Shape :
  val s = to-seq(shapes)
  rem(next(s), s)

public defn offset (s:Shape, o:Float) -> Shape :
  Shape(fn (p): fab(s, p) - lit(o), fatten(bounds(s), o), points(s))
  
public defn clearance (a:Shape, b:Shape, o:Float) -> Shape :
  Shape(a & invert(offset(b, (- o))), bounds(a), points(a))
  
public defn shell (s:Shape, o:Float) -> Shape :
  clearance(s, s, o)
  
public defn blend (a:Shape, b:Shape, m:Float) -> Shape :
  val joint = a | b
  val fillet = Shape(fn (p): sqrt(abs(fab(a, p))) + sqrt(abs(fab(b, p))) - lit(m), bounds(a) | bounds(b))
  joint | fillet
  
public defn morph (a:Shape, b:Shape, m:Float) -> Shape :
  Shape(fn (p): fab(a, p) * (lit(1.0) - lit(m)) + fab(b, p) * lit(m), bounds(a) | bounds(b))
  
defn rect (xmin:Float, ymin:Float, xmax:Float, ymax:Float) -> Shape :
  val b = Box(V3f(xmin, ymin, FLOAT-MIN), V3f(xmax, ymax, FLOAT-MAX))
  Shape(fn (p): max(max(lit(xmin) - x(p), x(p) - lit(xmax)), max(lit(ymin) - y(p), y(p) - lit(ymax))),
        Box(V3f(xmin, ymin, FLOAT-MIN), V3f(xmax, ymax, FLOAT-MAX)))

public defn dim-min (d:Float) -> Float : d * -0.5f
public defn dim-max (d:Float) -> Float : d *  0.5f

public defn dim-min (dim:V3f) -> V3f : V3f(dim-min(x(dim)), dim-min(y(dim)), dim-min(z(dim)))
public defn dim-max (dim:V3f) -> V3f : V3f(dim-max(x(dim)), dim-max(y(dim)), dim-max(z(dim)))

public defn rect (w:Float, h:Float) -> Shape :
  rect(dim-min(w), dim-min(h), dim-max(w), dim-max(h))

public defn rect (v0:V3f, v1:V3f, d:Float) -> Shape :
  val v = v1 - v0
  val a = atan2(y(v),x(v))
  mov(0.5f * (v0 + v1), rot-z(radians-to-degrees(a), rect(magnitude(v) + d, d)))

public defn square (d:Float) -> Shape :
  rect(d, d)

public defn edge (v0:V3f, v1:V3f) -> Shape :
  Shape(fn (p): lit(y(v1) - y(v0)) * (x(p) - lit(x(v0))) - lit(x(v1) - x(v0)) * (y(p) - lit(y(v0))),
        Box(v0) | Box(v1))

public defn convex (pts:Tuple<V3f>) -> Shape :
  val edges = for i in 0 to length(pts) seq :
                edge(pts[i], pts[(i + 1) % length(pts)])
  reduce(fn (a:Shape, b:Shape): a & b, edges)

public defn to-field2 (m:Mesh) -> Shape :
  val verts = vertices(m)
  union(for face in faces(m) seq :
          triangle(verts[face[0]], verts[face[1]], verts[face[2]]))

public defn to-field2 (p:Polygon) -> Shape :
  to-field2(triangulate(p))

public defn triangle (a:V3f, b:V3f, c:V3f) -> Shape :
  convex([a, b, c])

defn extrude (zmin:Float, zmax:Float, shape:Shape) -> Shape :
  val b = bounds(shape)
  Shape(fn (p): max(fab(shape, p), max(lit(zmin) - z(p), z(p) - lit(zmax))),
        Box(V3f(x(lo(b)), y(lo(b)), zmin), V3f(x(hi(b)), y(hi(b)), zmax)))

public defn extrude (h:Float, shape:Shape) -> Shape :
  extrude(dim-min(h), dim-max(h), shape)

;;; DEPRECATE
defn cube (xmin:Float, ymin:Float, zmin:Float, xmax:Float, ymax:Float, zmax:Float) -> Shape :
  extrude(zmin, zmax, rect(xmin, ymin, xmax, ymax))

;;; DEPRECATE
public defn cube (w:Float, h:Float, d:Float) -> Shape :
  cube(dim-min(w), dim-min(h), dim-min(d), dim-max(w), dim-max(h), dim-max(d))

public defn cube (d:Float) -> Shape :
  cube(d, d, d)

public defn cube (xy:Float, z:Float) -> Shape :
  cube(xy, xy, z)

public defn cube (v:V3f) -> Shape :
  cube(x(v), y(v), z(v))

public defn box (dims:V3f) -> Shape :
  defn fab (p) :
    val d = abs(p) - lit(0.5f * dims)
    min(max(x(d),max(y(d),z(d))),lit(0.0)) + len(max(d,lit(0.0)))  
  Shape(fab, dims-to-box(dims))

public defn box (b:Box) -> Shape :
  cube(x(lo(b)), y(lo(b)), z(lo(b)), x(hi(b)), y(hi(b)), z(hi(b)))

public defn round-box (dims:V3f, r:Float) -> Shape :
  Shape(fn (p): len(max(abs(p) - lit(0.5) * lit(dims), lit(0.0))) - lit(r), dims-to-box(dims))

public defn cylinder (d:Float, h:Float) -> Shape :
  extrude(h, circle(d))

public defn capsule (a:V3f, b:V3f, d:Float) -> Shape :
  val r  = 0.5f * d
  defn fab (p) :
    val pa = p - lit(a)
    val ba = b - a
    val h  = clamp(dot(pa, lit(ba)) / lit(dot(ba, ba)), lit(0.0), lit(1.0))
    len(pa - h * lit(ba)) - lit(r)
  Shape(fab, fatten(Box(a, a) | Box(b, b), r))

public defn capsule (d:Float, h:Float) -> Shape :
  val r = 0.5f * h
  cylinder(d, h) | mov-z(r, sphere(d)) | mov-z((- r), sphere(d))

public defn capsule2 (a:V3f, b:V3f, d:Float) -> Shape :
  val r  = 0.5f * d
  defn fab (p) :
    val pa = xy(p) - lit(a)
    val ba = b - a
    val h  = clamp(dot(pa, lit(ba)) / lit(dot(ba, ba)), lit(0.0), lit(1.0))
    len(pa - h * lit(ba)) - lit(r)
  Shape(fab, fatten(Box(a, a) | Box(b, b), r))

public defn thicken (segments:Seqable<[V3f, V3f]>, f:(V3f, V3f, Float) -> Shape, d:Float) -> Shape :
  val caps = for [v0, v1] in segments seq : f(v0, v1, d)
  reduce(fn (a:Shape, b:Shape): a | b, caps)

public defn thicken (g:PolyLine|Contour|Polygon, f:(V3f, V3f, Float) -> Shape, d:Float) -> Shape :
  thicken(segments(g), f, d)

public defn cone (d:Float, h:Float) -> Shape :
  Shape(taper-xy-z(h, 1.0f, 0.0f, cylinder(d, h)),
        dims-to-box(V3f(d, d, h)))

public defn pyramid (d:Float, h:Float) -> Shape :
  Shape(taper-xy-z(h, 1.0f, 0.0f, extrude(h, rect(d, d))),
        dims-to-box(V3f(d, d, h)))

public defn torus (D:Float, d:Float) -> Shape :
  Shape(fn (p): len(Vec2Field(lit(D * 0.5f) - len(xy(p)), z(p))) - lit(d * 0.5f),
        dims-to-box(V3f(D + d, D + d, d)))

public defn space (gdims:V3f, shape:Shape) :
  val gratio = gdims / dims(bounds(shape))
  val fac    = min(gratio)
  mag1(fac, shape)

public defn empty () : Shape(lit{FLOAT-MAX}, min-box())
public defn full () : Shape(lit{FLOAT-MIN}, inf-box())

public defn space-x (gdim:Float, shape:Shape) : mag1(gdim / x(dims(bounds(shape))), shape)
public defn space-y (gdim:Float, shape:Shape) : mag1(gdim / y(dims(bounds(shape))), shape)
public defn space-z (gdim:Float, shape:Shape) : mag1(gdim / z(dims(bounds(shape))), shape)

public defmulti spacer<?T> (g:?T&Poseable, dims:V3f) -> T
defmethod spacer (g:Shape, gdims:V3f) : Shape(lit{FLOAT-MAX}, Box(dim-min(gdims), dim-max(gdims)))
defmethod spacer (g:Assembly, dims:V3f) : Assembly(BLACK, [ spacer(empty(), dims) ])

public defn spacer-x<?T> (gdim:Float, g:?T&Poseable) : spacer(g, V3f(gdim, 0.0f, 0.0f))
public defn spacer-y<?T> (gdim:Float, g:?T&Poseable) : spacer(g, V3f(0.0f, gdim, 0.0f))
public defn spacer-z<?T> (gdim:Float, g:?T&Poseable) : spacer(g, V3f(0.0f, 0.0f, gdim))

public defn spacer-x (gdim:Float) : spacer-x(gdim, empty())
public defn spacer-y (gdim:Float) : spacer-y(gdim, empty())
public defn spacer-z (gdim:Float) : spacer-z(gdim, empty())

public defn stack-idx<?T> (ishapes:Seqable<?T&Poseable>, idx:Int, dir:Float) -> Seq<T> :
  val shapes = to-tuple(ishapes)
  val tot = reduce(plus, for shape in shapes seq : dims(bounds(shape))[idx])
  var tot-fac = 0.0f
  val res =
    generate<T&Poseable> :
      for (child in shapes, i in 0 to false) do :
        val now-fac = dims(bounds(child))[idx] / tot
        val ctr     = center(bounds(child))[idx]
        val amount  = dir * (tot / 2.0f - (tot-fac + now-fac / 2.0f) * tot) - ctr
        val del     = amount * V3f-unit(idx)
        yield(mov(del, child))
        tot-fac = tot-fac + now-fac
  filter({ volume(dims(bounds(_))) > 0.0f }, res)

public defn stack-x<?T> (shapes:Seqable<?T&Poseable>) -> Seq<T> :
  stack-idx(shapes, 0,  1.0f)

public defn stack-y<?T> (shapes:Seqable<?T&Poseable>) -> Seq<T> :
  stack-idx(shapes, 1,  1.0f)

public defn stack-z<?T> (shapes:Seqable<?T&Poseable>) -> Seq<T> :
  stack-idx(shapes, 2,  1.0f)

public deftype AnyGlue <: Poseable
public defmulti amount (g:AnyGlue) -> Float

public deftype Glue <: AnyGlue & Shape

public defn glue-amount<?T> (shape:?T&Poseable) -> Float :
  match(shape) :
    (g:AnyGlue) : amount(g)
    (s)         : 0.0f

public defn Glue (amount:Float) :
  new Glue :
    defmethod fabber (this): fn (p:Vec3Field) : lit(FLOAT-MAX)
    defmethod bounds (this): Box(V3f(0.0f, 0.0f, 0.0f), V3f(0.0f, 0.0f, 0.0f))
    defmethod amount (this): amount

public deftype AssemblyGlue <: AnyGlue & Assembly
defmulti glue (g:AssemblyGlue) -> Glue

public defn AssemblyGlue (glue:Glue) :
  new AssemblyGlue :
    defmethod glue (this) : glue
    defmethod color (this): BLACK
    defmethod bounds (this) : bounds(glue)
    defmethod amount (this) : amount(glue)

public defmulti glue-of<?T> (s:?T&Poseable, a:Float) -> T
defmethod glue-of (s:Shape, a:Float) -> Shape : Glue(a)
defmethod glue-of (s:Assembly, a:Float) -> Assembly : AssemblyGlue(Glue(a))

defn glue-when<?T> (zero?:True|False, shapes:Seqable<?T&Poseable>) -> Seq<T> :
  generate<T> :
    for (shape in shapes, i in 0 to false) do :
      if zero? or i != 0: yield(glue-of(shape, 1.0f))
      yield(shape)

public defn glue-even<?T> (shapes:Seqable<?T&Poseable>) -> Seq<T> :
  glue-when(true, shapes)

public defn glue-odd<?T> (shapes:Seqable<?T&Poseable>) -> Seq<T> :
  glue-when(false, shapes)

public defn glue<?T> (shapes:Seqable<?T&Poseable>) -> Seq<T> :
  glue-odd(shapes)

public defn assign-glue-idx<?T> (ishapes:Seqable<?T&Poseable>, target:Float, idx:Int) -> Seq<T> :
  val shapes = to-tuple(ishapes)
  val tot = reduce(plus, for shape in shapes seq : dims(bounds(shape))[idx])
  val glue-tot = reduce(plus, for shape in shapes seq : glue-amount(shape))
  val gap-tot = (target - tot)
  for shape in shapes seq :
    match(shape) :
      (g:AnyGlue&T) :
        val s = (amount(g) / glue-tot) * gap-tot * V3f-unit(idx)
        spacer(shape, s)
      (s)           : s

public defn stack-idx<?T> (d:Float, idx:Int, shapes:Seqable<?T&Poseable>) -> Seq<T> :
  stack-idx(assign-glue-idx(shapes, d, idx), idx, 1.0f)

public defn stack-x<?T> (w:Float, shapes:Seqable<?T&Poseable>) -> Seq<T> :
  stack-x(assign-glue-idx(shapes, w, 0))

public defn stack-y<?T> (h:Float, shapes:Seqable<?T&Poseable>) -> Seq<T> :
  stack-y(assign-glue-idx(shapes, h, 1))

public defn stack-z<?T> (d:Float, shapes:Seqable<?T&Poseable>) -> Seq<T> :
  stack-z(assign-glue-idx(shapes, d, 2))

defn align-it<?T> (ishapes:Seqable<?T&Poseable>, value:(Box) -> Float, mov:(Float, T) -> T) -> Seq<T> :
  val shapes = to-tuple(ishapes)
  val target = reduce(union, seq(bounds, shapes))
  ;; val target = bounds(shapes[0])
  for (child in shapes) seq :
    mov(value(target) - value(bounds(child)), child)

public defn attach<?T> (src:?T&Poseable, spos:V3f, dst:?T&Poseable, dpos:V3f) -> Seqable<T> :
  [mov(dpos - spos, src), dst]

public defn attach<?T> (src:?T&Poseable, si:Int, dst:?T&Poseable, di:Int) -> Seqable<T> :
  attach(src, points(src)[si], dst, points(dst)[di])

public defn align-min-x<?T> (shapes:Seqable<?T&Poseable>) -> Seq<T> :
  align-it(shapes, { x(lo(_)) }, mov-x)

public defn align-max-x<?T> (shapes:Seqable<?T&Poseable>) -> Seq<T> :
  align-it(shapes, { x(hi(_)) }, mov-x)

public defn align-min-y<?T> (shapes:Seqable<?T&Poseable>) -> Seq<T> :
  align-it(shapes, { y(lo(_)) }, mov-y)

public defn align-max-y<?T> (shapes:Seqable<?T&Poseable>) -> Seq<T> :
  align-it(shapes, { y(hi(_)) }, mov-y)

public defn align-min-z<?T> (shapes:Seqable<?T&Poseable>) -> Seq<T> :
  align-it(shapes, { z(lo(_)) }, mov-z)

public defn align-max-z<?T> (shapes:Seqable<?T&Poseable>) -> Seq<T> :
  align-it(shapes, { z(hi(_)) }, mov-z)

public defmulti jit-poseable (p:Poseable) -> AnyTree

defmethod jit-poseable (s:Shape) -> BoundedTree :
  val e = fab(s, Vec3Field(fab-x(), fab-y(), fab-z()))
  ;; println-all(["E = " e])
  BoundedTree(bounds(s), tree(e))

defmethod jit-poseable (a:Assembly) -> TreeAssembly :
  TreeAssembly(mat(a), color(a), to-tuple $ seq(jit-poseable, children(a)))

public defn jit (g:Geom) -> Geom :
  match(g) :
    (p:Poseable) :  jit-poseable(p)
    (geom) :        geom

public defn slice (z:Float, s:Shape) -> Polygon :
  val bt = jit(s) as BoundedTree
  val bds = Box(xy(lo(bounds(s)) + V3f(0.0f, 0.0f, z)), xy(hi(bounds(s)) + V3f(0.0f, 0.0f, z)))
  val poly = tree-render-slice(tree(bt), bds, 128.0f)
  poly

public defn save (z:Float, filename:String, s:Shape) -> False :
  val bt = jit(s) as BoundedTree
  tree-export-slice(tree(bt), filename, bounds(s), 128.0f)

public defn save (filename:String, s:Shape) -> False :
  val bt = jit(s) as BoundedTree
  tree-export-mesh(tree(bt), filename, bounds(s), 128.0f)

;;;

val big = 1.0e6f

public defn join-pieces (a:Shape, b:Shape, get-slots-info:(Float, Float) -> [Int, Float, Float]) -> [Shape, Shape] :
  val overlap   = intersection $ [a, b]
  val dims      = dims(bounds(overlap))
  val idx       = max-idx(dims)
  val [inslots, slot-off, slot-size] = get-slots-info(lo(bounds(overlap))[idx], dims[idx])
  val nslots    = inslots when inslots > 0 else to-int $ ceil $ dims[idx] / slot-size 
  val num       = to-int $ round $ (to-float(nslots) / 2.0f)
  val slot-dim  = (dims[idx] / to-float(nslots)) when inslots >= 0 else slot-size
  ;; println-all(["INSLOTS " inslots " NSLOTS " nslots " SLOT-SIZE " slot-size " SLOT-DIM " slot-dim " NUM " num])
  val tdims     = set-elt(V3f-fill(big),  idx, slot-dim)
  val vslot-off = set-elt(V3f-fill(0.0f), idx, (- slot-off))
  val tooth     = cube(tdims)
  val tooths    = union $ stack-idx(slot-dim * to-float(nslots), idx, glue-when(nslots % 2 == 0, seq({ tooth }, 0 to num)))
  ;; println-all(["NUM " num " NSLOTS " nslots " IDX " idx " DIMS " dims " TDIMS " tdims[idx]])
  val teeth     = overlap & mov(center(bounds(overlap)) + vslot-off, tooths)
  val pa        = rem(a, teeth)
  val pb        = rem(b, pa)
  [pa, pb]

public defn all-combos (a:Seqable<Int>, b:Seqable<Int>) -> Seqable<[Int, Int]> :
  generate<[Int, Int]> :
    for i in a do :
      for j in b do :
        yield([i, j])

public defn captured-on? (i:Box, a:Box, d:Int) :
  val eps = 0.001f
  val res = abs(lo(i)[d] - lo(a)[d]) > eps and abs(hi(a)[d] - hi(i)[d]) > eps
  ;; println-all(["CAPTURED-ON? " i " / " a " D " d " -> " res])
  res

public defn captured? (b1:Box, b2:Box) -> True|False :
  defn any-captured? (i:Box, a:Box, big-d:Int) :
    val res = any?(captured-on?{i, a, _}, filter({ _ != big-d}, 0 to 3))
    ;; println-all(["ANY-CAPTURED? " i " / " a " big-d " big-d " -> " res])
    res
  val intersection = b1 & b2
  val big-d = max-idx(dims(intersection))
  any-captured?(intersection, b1, big-d) and any-captured?(intersection, b2, big-d)

defn n-smalls (dims:V3f) :
  val elts = to-tuple $ lazy-qsort(seq(get{dims, _}, 0 to 3))
  val n    = for d in 1 to 3 count : elts[0] == elts[d]
  n + 1

public defn captured2d? (b1:Box, b2:Box) -> True|False :
  if n-smalls(dims(b1 & b2)) == 1 :
    val i = b1 & b2
    val [d0, d1] = [mid-idx(dims(i)), max-idx(dims(i))]
    val c0 = (captured-on?(i, b1, d0) and captured-on?(i, b2, d1))
    val c1 = (captured-on?(i, b1, d1) and captured-on?(i, b2, d0))
    c0 or c1
  else :
    false

public defn join-pieces (elts:Seqable<Shape>,
                         default-slot-info-getter:((Float, Float) -> [Int, Float, Float]),
                         slots-info-getters:Tuple<KeyValue<[Int,Int],((Float, Float) -> [Int, Float, Float])>>,
                         sames:Tuple<[Int, Int, V3f]>) :
  defn lookup-slot-info-getter (i:Int, j:Int) -> ((Float,Float) -> [Int, Float, Float]) :
    label<((Float,Float) -> [Int, Float, Float])> return :  
      for kv in slots-info-getters do :
        val [ki, kj] = key(kv)
        if (ki == i and kj == j) or (ki == j and kj == i) :
          return(value(kv))
      default-slot-info-getter
  val pieces = to-array<Shape> $ elts
  defn update (k:Int, nk:Shape) :
    pieces[k] = nk
    for [i, j, d] in sames do :
      if i == k :
        println-all(["SAME UPDATE " j " <- " i])
        pieces[j] = pieces[j] & mov(d, pieces[i])
  for i in 0 to length(pieces) do :
    for j in (i + 1) to length(pieces) do :
      val [bi, bj] = [bounds(pieces[i]), bounds(pieces[j])]
      if not empty?(bi & bj) :
        val num = (fn (o:Float,s:Float) : [2, 0.0f, 0.0f]) when captured?(bi, bj) else lookup-slot-info-getter(i, j)
        println-all(["I " i " J " j " NUM " num])
        val [ni, nj] = join-pieces(pieces[i], pieces[j], num)
        update(i, ni)
        update(j, nj)
  pieces

defn split-overlap (s0:Shape, s1:Shape, t:Float) :
  val i = bounds(s0) & bounds(s1)
  val d = max-idx(dims(bounds(s0)))
  val o = dims(i)[d] / 3.0f
  val s0a = s0 & box(Box(lo(bounds(s0)), set-elt(hi(bounds(s0)), d, lo(i)[d] + o)))
  val s0b = s0 & box(Box(set-elt(lo(bounds(s0)), d, hi(i)[d] - o), hi(bounds(s0))))
  [s0a s0b]

public defn split-overlaps (elts:Seqable<Shape>, t:Float) -> Seqable<Shape> :
  val pieces = to-vector<Shape> $ elts
  ;; println-all(["SPLIT-OVERLAPS IN " length(pieces)])
  let loop () :
    val restart? = label<True|False> restart:
      for i in 0 to length(pieces) do :
        for j in (i + 1) to length(pieces) do :
          val [bi, bj] = [bounds(pieces[i]), bounds(pieces[j])]
          ;; println-all(["I " i " J " j " BI " bi " BJ " bj])
          if not empty?(bi & bj) :
            if captured2d?(bi, bj) :
              val [a, b] = split-overlap(pieces[i], pieces[j], t)
              ;; println-all([  "  SPLIT " bounds(a) " / " bounds(b)])
              pieces[i] = a
              add(pieces, b)
              restart(true)
      false
    if restart? : loop()
  ;; println-all(["  OUT " length(pieces)])
  pieces

defn quilt-one (s:Shape, mx:Float, d0:Int, d1:Int, t:Float) -> Seqable<Shape> :
  val bds   = bounds(s)
  val dms   = dims(bds)
  val n = to-int $ round(dms[d0] / (mx + 2.0f * t))
  val g = dms[d0] / to-float(n)
  val o = lo(bds)[d0]
  val [ni, phase] = [1, -0.5f] when d1 == 1 else [0, 0.0f]
  val res = to-tuple $ for i in 0 to (n + ni) seq :
    val l = o + ((to-float(i) + phase)     * g - t)
    val h = o + ((to-float(i + 1) + phase) * g + t)
    s & box(Box(set-elt(lo(bds), d0, l), set-elt(hi(bds), d0, h)))
  println-all(["SPLIT MX " mx " D0 " d0 " D1 " d1 " PHASE " phase " -> " to-tuple $ seq(bounds, res)])
  res

;;; SPLITS BIG PIECES INTO N PIECES ACCORDING TO 
public defn quilt (shape:Shape, stock-dims:V3f) -> Seqable<Shape> :
  val t = min(stock-dims)
  val shape-dims = dims(bounds(shape))
  val sorted-shape-dims = to-tuple $ lazy-qsort(shape-dims)  
  val sorted-stock-dims = to-tuple $ lazy-qsort(stock-dims)
  val cmps = to-tuple $ seq({1 when _ <= _ else 0}, sorted-shape-dims, sorted-stock-dims)
  val n = reduce(plus, cmps)
  println-all(["N " n " SHAPE-DIMS " sorted-shape-dims " STOCK-DIMS " sorted-stock-dims " CMPS " cmps])
  if n == 3 :
    [ shape ]
  else if n == 2 :
    quilt-one(shape, sorted-stock-dims[1], max-idx(shape-dims), mid-idx(shape-dims), t)
  else :
    val splits-0 = quilt-one(shape, sorted-stock-dims[2], max-idx(shape-dims), mid-idx(shape-dims), t)
    to-tuple $ cat-all $ seq(quilt-one{_, sorted-stock-dims[1], mid-idx(shape-dims), max-idx(shape-dims), t}, splits-0)

public defn join-pieces (elts:Seqable<Shape>, num:(Float,Float) -> [Int, Float, Float]) :
  join-pieces(elts, num, [], [])

;; TODO: LIMITED TO AXIS ALIGNED SHAPES
public defn smash (s:Shape) -> Shape :
  val bb    = bounds(s)
  val dims  = dims(bb)
  val idx   = min-idx(dims)
  val ctr   = center(bb)
  defn rot (s) : if idx == 0 : reflect-xz(s) else if idx == 1 : reflect-yz(s) else : s
  rot(mov((- ctr), s))

defn segment<?T> (shapes:Seqable<?T>, width:T -> Float, max-width:Float, space:Float) -> Seqable<Tuple<T>> :
  val row = Vector<T>()
  generate<Tuple<T>> :
    for pc in shapes do :
      val off = reduce(plus, 0.0f, join(seq(width, row), space))
      if off > max-width :
        yield(to-tuple $ row)
        clear(row)
      add(row, pc)
    if length(row) > 0 : yield(to-tuple $ row)

public defn nest (pcs:Seqable<Shape>, size:V3f, space:Float) -> Seq<Assembly> :
  val spcs  = to-tuple $ lazy-qsort({ area(dims(bounds(_))) }, pcs)
  val rpcs  = seq(fn (pc): reflect-xy(pc) when max-idx(dims(bounds(pc))) != max-idx(size) else pc, spcs)
  val rows  = to-tuple $ segment(rpcs, { x(dims(bounds(_))) }, x(size), space)
  val pages = segment(rows, { y(dims(bounds(union(_)))) }, y(size), space)
  for page in pages seq :
    val rrows = for row in page seq: Assembly(stack-x(join(row, spacer-x(space))))
    val lay   = stack-y(join(rrows, Assembly([ spacer-y(space) ])))
    Assembly(cat-all $ seq(children, lay))

defn num-to-bits (num:Int) -> Tuple<Int> :
  to-tuple $ for i in 2 through 0 by -1 seq : (num >> i) & 1

defn num-to-bitz (num:Int) -> Tuple<Tuple<Int>> :
  to-tuple $ for i in 2 through 0 by -1 seq : num-to-bits((num >> (i * 3)) & 7)

defn bits-to-num (bitz:Tuple<Int>) -> Int :
  reduce(fn (a, x): a * 2 + x, bitz)

defn bitz-to-num (bitz:Tuple<Tuple<Int>>) -> Int :
  reduce(fn (a, x): a * 8 + x, seq(bits-to-num, bitz))

defn canonicalize-bits (bits:Tuple<Int>) -> Tuple<Int> :
  val rbits = [bits[2], bits[1], bits[0]]
  if bits-to-num(bits) < bits-to-num(rbits): bits else: rbits

defn canonicalize-bitz (bitz:Tuple<Tuple<Int>>) -> Tuple<Tuple<Int>> :
  to-tuple $ seq(num-to-bits, lazy-qsort(seq(bits-to-num, seq(canonicalize-bits, bitz))))

defn check (form:Tuple<Tuple<Int>>) -> True|False :
  defn boxed? (dim:Int, idx:Int) :
    label<True|False> return :
      val dn? = for i in idx through 0 by -1 any? :
        form[dim][i] == 1
      val up? = for i in (idx + 1) to 3 any? :
        form[dim][i] == 1
      dn? and up?
  label<True|False> return :
    for i in 0 to 2 do :
      for j in 0 to 2 do :
        for k in 0 to 2 do :
          if boxed?(0, i) and boxed?(1, j) and boxed?(2, k) :
            return(false)
    true

public defn all-forms () :
  val forms = HashTable<Int, Tuple<Tuple<Int>>>()
  for i in 0 to (2 << 8) do :
    val bitz  = num-to-bitz $ i
    val cbitz = canonicalize-bitz $ bitz
    val cnum  = bitz-to-num $ cbitz
    val ok?   = count({ _ == 0}, seq(bits-to-num, cbitz)) <= 1
    val ck?   = check(cbitz)
    if ck? and ok? and not key?(forms, cnum) :
      ;; println-all(["I " i " CNUM " cnum " BITZ " bitz " CBITZ " cbitz " CK? " ck? " OK? " ok? " KEY? " key?(forms, cnum)])
      forms[cnum] = cbitz
  forms

